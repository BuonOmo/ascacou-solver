#!/usr/bin/env zsh
#
# Usage: ./tourney <commit>

set -e

(($# == 1)) || {
	echo "Usage: $0 <commit>"
	exit 1
}

target_dir=${CARGO_TARGET_DIR:-target}
commit="$1"

## Setup

# Generate benchmarks/data
[[ -d "benchmarks/data" ]] || cargo run --bin generate

# Build previous version
[[ -f "$target_dir/$commit/minicou" ]] || {
	should_stash=$(git diff --exit-code --quiet && echo false || echo true)
	if [ "$should_stash" = true ]; then
		git stash push -m "tourney temporary stash"
	fi
	git checkout $1
	cargo build --release -Zunstable-options --features=cli --bin=minicou --artifact-dir $target_dir/$commit
	git checkout -
	if [ "$should_stash" = true ]; then
		git stash pop
	fi
}

# Build current version
cargo build --release --features=cli --bin=minicou

# Build game-info tool
cargo build --release --bin=game-info

## Play

typeset -A counts=(
	"endgame" 20
	"midgame" 5
	"earlygame" 3
	"startgame" 3
)
new="$target_dir/release/minicou"
old="$target_dir/$commit/minicou"
game_info="$target_dir/release/game-info"
data=(benchmarks/data/{endgame,midgame,earlygame,startgame})
typeset -A ais=(-1 "$old" 1 "$new")
typeset -A names=(-1 "$commit" 1 "$(git rev-parse --abbrev-ref HEAD)")
total=0

sign() {
	if (($1 > 0)); then
		echo 1
	elif (($1 < 0)); then
		echo -1
	else
		echo 0
	fi
}

next_move() {
	$game_info "$1" "$2"
}
is_terminal() {
	$game_info "$1" | awk '{print $1}' | grep -q '^true$'
}
score() {
	$game_info "$1" | awk '{print $2}'
}

play() {
	$1 --timeout=1000 --depth=51 $2 | awk '/Move:/ {print $2}'
}

game() {
	local board="$1"
	local order="$2"

	while ! is_terminal "$board"; do
		move=$(play "${ais[$order]}" "$board")
		board=$(next_move "$board" "$move")
		order=$((-order))
	done
	echo $(($(score "$board") * order))
}

for file in $data; do
	setname=${file:t}
	for order in -1 1; do
		setscore=0
		echo -n "Playing ${counts[$setname]} games from $setname set, ${names[$order]} plays first... "
		while read -r board; do
			result=$(game "$board" "$order")
			setscore=$((setscore + result))
		done < <(head -n "${counts[$setname]}" "$file")
		echo "Score: $setscore"
		total=$((total + setscore))
	done
	echo
done

echo "Final score: $total"

winner=$(sign $total)
case $winner in
0) echo "Draw: both versions may be the same..." ;;
*) echo "${names[$winner]} wins!" ;;
esac
